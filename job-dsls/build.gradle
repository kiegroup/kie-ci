apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'
// Some bogus artifact which dissappeared from the remote repos. Not needed anyway.
configurations.all*.exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.13'
    implementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    implementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'cglib:cglib-nodep:3.3.0' // used by Spock

    // Jenkins test harness dependencies
    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:2.66'
    testImplementation "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    //testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testImplementation 'org.jenkins-ci.plugins:structs:1.23@jar'
    testImplementation "org.jenkins-ci.plugins:config-file-provider:3.8.1@jar"
    testImplementation 'org.jenkins-ci.plugins:credentials:2.6.1@jar'
    testImplementation 'org.jenkins-ci.plugins:credentials-binding:1.27@jar'
    testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:6.740.ve4f4ffa_dea_54'
    testImplementation 'org.jenkins-ci.plugins:multiple-scms:0.8@jar'
    testImplementation 'org.jenkins-ci.plugins:plain-credentials:1.7@jar'
    testImplementation 'org.jenkins-ci.plugins:ssh-credentials:1.19@jar'
    testImplementation 'org.jenkins-ci.plugins:token-macro:267.vcdaea6462991@jar'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6@jar'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.24@jar'

    // plugins to install in test instance (all plugins used by the DSL scripts needs to be specified)
    // keep the alphabetical order
    testPlugins 'com.cloudbees.plugins:build-flow-plugin:0.20'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.34.3.1'
    // should be 1.5.16 but 1.5.16 is missing in the repo
    // testPlugins 'com.redhat.jenkins.plugins:redhat-ci-plugin:1.5.10'
    testPlugins 'com.sonyericsson.hudson.plugins.rebuild:rebuild:1.34'
    testPlugins 'com.synopsys.jenkinsci:ownership:0.13.0'
    testPlugins 'io.jenkins.plugins:warnings-ng:9.0.1'
    testPlugins 'org.jenkins-ci.main:maven-plugin:3.19'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.16'
    testPlugins 'org.jenkins-ci.plugins:ansicolor:1.0.2'
    testPlugins 'org.jenkins-ci.plugins:build-timeout:1.21'
    testPlugins 'org.jenkins-ci.plugins:config-file-provider:3.10.0'
    testPlugins 'org.jenkins-ci.plugins:credentials:1139.veb_9579fca_33b_'
    testPlugins 'org.jenkins-ci.plugins:credentials-binding:523.vd859a_4b_122e6'
    testPlugins 'org.jenkins-ci.plugins:description-setter:1.10'
    testPlugins 'org.jenkins-ci.plugins:email-ext:2.91'
    testPlugins 'org.jenkins-ci.plugins:envinject:2.875.v9b_9e962da_a_ec'
    testPlugins 'org.jenkins-ci.plugins:generic-webhook-trigger:1.72'
    testPlugins 'org.jenkins-ci.plugins:git:4.11.4'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.2'
    testPlugins 'org.jenkins-ci.plugins:github-branch-source:2.10.0'
    testPlugins 'org.jenkins-ci.plugins:groovy:442.v817e6d937d6c'
    testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.1.27'
    testPlugins 'org.jenkins-ci.plugins:junit:1119.1121.vc43d0fc45561'
    testPlugins 'org.jenkins-ci.plugins:mask-passwords:3.0'
    testPlugins 'org.jenkins-ci.plugins:matrix-project:772.v494f19991984'
    testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.8'
    testPlugins 'org.jenkins-ci.plugins:nested-view:1.19'
    testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.45'
    testPlugins 'org.jenkins-ci.plugins:pipeline-build-step:2.18'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-declarative-agent:1.1.1'
    testPlugins 'org.jenkins-ci.plugins:pipeline-input-step:2.12'
    testPlugins 'org.jenkins-ci.plugins:pipeline-maven:1161.v89a_7dcec5d31'
    testPlugins 'org.jenkins-ci.plugins:pipeline-stage-step:293.v200037eefcd5'
    testPlugins 'org.jenkins-ci.plugins:pipeline-utility-steps:2.13.0'
    testPlugins 'org.jenkins-ci.plugins:pipeline-graph-analysis:195.v5812d95a_a_2f9'
    testPlugins 'org.jenkins-ci.plugins:pipeline-milestone-step:101.vd572fef9d926'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-api:2.2114.v2654ca_721309'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-definition:2.2114.v2654ca_721309'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-extensions:2.2114.v2654ca_721309'
    testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.24'
    testPlugins 'org.jenkins-ci.plugins:script-security:1175.v4b_d517d6db_f0'
    testPlugins 'org.jenkins-ci.plugins:ssh-agent:295.v9ca_a_1c7cc3a_a_'
    testPlugins 'org.jenkins-ci.plugins:ssh-credentials:277.280.v1e86b_7d0056b_'
    testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:2.8'
    testPlugins 'org.jenkins-ci.plugins:timestamper:1.18'
    testPlugins 'org.jenkins-ci.plugins:token-macro:308.v4f2b_ed62b_b_16'
    testPlugins 'org.jenkins-ci.plugins:toolenv:1.2'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:590.v6a_d052e5a_a_b_5'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:639.v6eca_cd8c04a_a_'
    testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.42'
    testPlugins 'org.jenkins-ci.plugins:xvnc:1.28'
    testPlugins 'org.jenkins-ci.plugins:hidden-parameter:0.0.4'
    testPlugins 'org.jvnet.hudson.plugins:ircbot:2.36'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapperTask(type: Wrapper) {
    gradleVersion = '6.6.1'
    distributionType = 'all'
}
