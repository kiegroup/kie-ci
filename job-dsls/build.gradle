apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
    maven {
        url 'https://repo.spring.io/plugins-release/'
    }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'
// Some bogus artifact which dissappeared from the remote repos. Not needed anyway.
configurations.all*.exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.55'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.13@jar'
    testCompile "org.jenkins-ci.plugins:config-file-provider:2.15.7@jar"
    testCompile 'org.jenkins-ci.plugins:credentials:2.1.13@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.12@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5@jar'

    // plugins to install in test instance (all plugins used by the DSL scripts needs to be specified)
    // keep the alphabetical order
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.12'
    testPlugins 'com.cloudbees.plugins:build-flow-plugin:0.20'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'com.sonyericsson.hudson.plugins.rebuild:rebuild:1.25'
    testPlugins 'com.synopsys.jenkinsci:ownership:0.9.1'
    testPlugins 'org.jenkins-ci.main:maven-plugin:2.15.1'
    testPlugins 'org.jenkins-ci.plugins:ansicolor:0.5.0'
    testPlugins 'org.jenkins-ci.plugins:build-timeout:1.18'
    testPlugins 'org.jenkins-ci.plugins:config-file-provider:2.15.7'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.13'
    testPlugins 'org.jenkins-ci.plugins:description-setter:1.10'
    testPlugins 'org.jenkins-ci.plugins:envinject:2.0'
    testPlugins 'org.jenkins-ci.plugins:git:3.2.0'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.36.1'
    testPlugins 'org.jenkins-ci.plugins:junit:1.20'
    testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.6'
    testPlugins 'org.jenkins-ci.plugins:nested-view:1.14'
    testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.26'
    testPlugins 'org.jenkins-ci.plugins:script-security:1.27'
    testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:1.9.0'
    testPlugins 'org.jenkins-ci.plugins:timestamper:1.8.8'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.1'
    testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.32'
    testPlugins 'org.jenkins-ci.plugins:xvnc:1.24'
    testPlugins 'org.jvnet.hudson.plugins:checkstyle:3.48'
    testPlugins 'org.jvnet.hudson.plugins:findbugs:4.70'
    testPlugins 'org.jvnet.hudson.plugins:ircbot:2.27'
    testPlugins 'org.jvnet.hudson.plugins:toolenv:1.0'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
    distributionType = 'all'
}
