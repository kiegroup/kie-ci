apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'
// Some bogus artifact which dissappeared from the remote repos. Not needed anyway.
configurations.all*.exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.13'
    implementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    implementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'cglib:cglib-nodep:3.3.0' // used by Spock

    // Jenkins test harness dependencies
    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:1873.va_ed9d52f2b_42'
    testImplementation "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    //testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testImplementation 'org.jenkins-ci.plugins:branch-api:2.1071.v1a_188a_562481@jar'    
    testImplementation "org.jenkins-ci.plugins:config-file-provider:3.11.1@jar"
    testImplementation 'org.jenkins-ci.plugins:credentials:1224.vc23ca_a_9a_2cb_0@jar'
    testImplementation 'org.jenkins-ci.plugins:credentials-binding:1.27@jar'
    testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:6.815.v0dd5a_cb_40e0e@jar'
    testImplementation 'org.jenkins-ci.plugins:multiple-scms:0.8@jar'
    testImplementation 'org.jenkins-ci.plugins:plain-credentials:143.v1b_df8b_d3b_e48@jar'
    testImplementation 'org.jenkins-ci.plugins:scm-api:631.v9143df5b_e4a_a@jar'
    testImplementation 'org.jenkins-ci.plugins:ssh-credentials:305.v8f4381501156@jar'
    testImplementation 'org.jenkins-ci.plugins:structs:324.va_f5d6774f3a_d@jar'    
    testImplementation 'org.jenkins-ci.plugins:token-macro:321.vd7cc1f2a_52c8@jar'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-aggregator:596.v8c21c963d92d@jar'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-step-api:639.v6eca_cd8c04a_a_@jar'

    // plugins to install in test instance (all plugins used by the DSL scripts needs to be specified)
    // keep the alphabetical order
    testPlugins 'com.cloudbees.plugins:build-flow-plugin:0.20'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.34.3.1'
    // should be 1.5.16 but 1.5.16 is missing in the repo
    // testPlugins 'com.redhat.jenkins.plugins:redhat-ci-plugin:1.5.10'
    testPlugins 'com.sonyericsson.hudson.plugins.rebuild:rebuild:1.34'
    testPlugins 'io.jenkins.plugins:warnings-ng:9.0.1'
    testPlugins 'org.jenkins-ci.main:maven-plugin:3.19'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.815.v0dd5a_cb_40e0e'
    testPlugins 'org.jenkins-ci.plugins:ansicolor:1.0.2'
    testPlugins 'org.jenkins-ci.plugins:build-timeout:1.28'
    testPlugins 'org.jenkins-ci.plugins:config-file-provider:3.11.1'
    testPlugins 'org.jenkins-ci.plugins:credentials:1224.vc23ca_a_9a_2cb_0'
    testPlugins 'org.jenkins-ci.plugins:credentials-binding:523.vd859a_4b_122e6'
    testPlugins 'org.jenkins-ci.plugins:description-setter:1.10'
    testPlugins 'org.jenkins-ci.plugins:email-ext:2.95'
    testPlugins 'org.jenkins-ci.plugins:envinject:2.901.v0038b_6471582'
    testPlugins 'org.jenkins-ci.plugins:generic-webhook-trigger:1.72'
    testPlugins 'org.jenkins-ci.plugins:git:5.0.0'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.2'
    testPlugins 'org.jenkins-ci.plugins:github-branch-source:2.10.0'
    testPlugins 'org.jenkins-ci.plugins:groovy:442.v817e6d937d6c'
    testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.1.27'
    testPlugins 'org.jenkins-ci.plugins:junit:1189.v1b_e593637fa_e'
    testPlugins 'org.jenkins-ci.plugins:mask-passwords:3.0'
    testPlugins 'org.jenkins-ci.plugins:matrix-project:785.v06b_7f47b_c631'
    testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.8'
    testPlugins 'org.jenkins-ci.plugins:nested-view:1.19'
    testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.45'
    testPlugins 'org.jenkins-ci.plugins:pipeline-build-step:487.va_823138eee8b_'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-declarative-agent:1.1.1'
    testPlugins 'org.jenkins-ci.plugins:pipeline-input-step:466.v6d0a_5df34f81'
    testPlugins 'org.jenkins-ci.plugins:pipeline-maven:1279.v5d711113020f'
    testPlugins 'org.jenkins-ci.plugins:pipeline-stage-step:305.ve96d0205c1c6'
    testPlugins 'org.jenkins-ci.plugins:pipeline-utility-steps:2.15.1'
    testPlugins 'org.jenkins-ci.plugins:pipeline-graph-analysis:202.va_d268e64deb_3'
    testPlugins 'org.jenkins-ci.plugins:pipeline-milestone-step:111.v449306f708b_7'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-api:2.2121.vd87fb_6536d1e'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-definition:2.2121.vd87fb_6536d1e'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-extensions:2.2121.vd87fb_6536d1e'
    testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.24'
    testPlugins 'org.jenkins-ci.plugins:script-security:1229.v4880b_b_e905a_6'
    testPlugins 'org.jenkins-ci.plugins:ssh-agent:327.v230ecd01f86f'
    testPlugins 'org.jenkins-ci.plugins:ssh-credentials:305.v8f4381501156'
    testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:2.11'
    testPlugins 'org.jenkins-ci.plugins:timestamper:1.22'
    testPlugins 'org.jenkins-ci.plugins:token-macro:321.vd7cc1f2a_52c8'
    testPlugins 'org.jenkins-ci.plugins:toolenv:1.2'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:596.v8c21c963d92d'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:639.v6eca_cd8c04a_a_'
    testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.44'
    testPlugins 'org.jenkins-ci.plugins:xvnc:1.28'
    testPlugins 'org.jenkins-ci.plugins:hidden-parameter:0.0.4'
    testPlugins 'org.jvnet.hudson.plugins:ircbot:2.36'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapperTask(type: Wrapper) {
    gradleVersion = '6.6.1'
    distributionType = 'all'
}
