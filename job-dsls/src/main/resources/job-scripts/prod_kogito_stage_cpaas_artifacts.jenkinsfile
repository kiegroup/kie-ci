@Library('jenkins-pipeline-shared-libraries')_

def artifacts = [
    [originClassifier: 'kogito-and-optaplanner-quickstarts.zip', finalClassifier: 'kogito-and-optaplanner-quickstarts.zip', info: 'Renaming quickstarts zip'],
    [originClassifier: 'maven-repository.zip', finalClassifier: 'kogito-maven-repository.zip', info: 'Renaming maven repository zip'],
    [originClassifier: 'license.zip', finalClassifier: 'kogito-licenses.zip', info: 'Renaming licenses zip'],
    [originClassifier: 'src.zip', finalClassifier: 'kogito-sources.zip', info: 'Renaming sources zip']
]

pipeline {
    agent {
        label 'kie-rhel7 && !built-in'
    }

    environment {
        PRODUCT_NAME = PRODUCT_NAME.trim()
        VERSION = VERSION.trim()
        RHPAM_VERSION = RHPAM_VERSION.trim()
        RCM_HOST = RCM_HOST.trim()
        STAGING_BASE_PATH = "${RCM_GUEST_FOLDER}"
        KOGITO_FOLDER_PATH = "${STAGING_BASE_PATH}/${PRODUCT_NAME}/${PRODUCT_NAME}-${VERSION}"
        KOGITO_FOLDER_PATH_RELATIVE = "staging/${PRODUCT_NAME}/${PRODUCT_NAME}-${VERSION}"
    }

    stages {
        stage('Clean workspace') {
            steps {
                cleanWs()
            }
        }

        stage('Print variables') {
            steps {
                println "[INFO] PRODUCT_NAME: ${PRODUCT_NAME}"
                println "[INFO] VERSION: ${VERSION}"
                println "[INFO] RHPAM_VERSION: ${RHPAM_VERSION}"
                println "[INFO] RCM_HOST: ${RCM_HOST}"
                println "[INFO] STAGING_BASE_PATH: ${STAGING_BASE_PATH}"
            }
        }

        stage('Clone integration-platform-tooling repository') {
            steps {
                sh "git clone ${INTEGRATION_PLATFORM_TOOLING_REPOSITORY} integration-platform-tooling"
            }
        }

        stage('Create offliner zip') {
            steps {
                script {
                    createOfflinerZip()
                }
            }
        }

        stage('Rename artifacts produced by CPaaS') {
            steps {
                script {
                    for (artifact in artifacts) {
                        renameArtifact(artifact)
                    }
                }
            }
        }

        stage('Generate maven repository text') {
            steps {
                script {
                    generateMavenRepositoryText()
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}

def createOfflinerZip() {
    println 'Creating offliner zip'
    def offlinerFolderName = "${PRODUCT_NAME}-${getMilestoneVersion(VERSION)}-offliner"
    def offlinerFolderPath = "${KOGITO_FOLDER_PATH}/${offlinerFolderName}"
    def offlinerFile = "${KOGITO_FOLDER_PATH}/offliner.txt"

    remoteExec("mkdir -p ${offlinerFolderPath}")
    remoteExec("cp ${offlinerFile} ${offlinerFolderPath}")
    util.withKerberos('rhba-prod-keytab') {
        def relativeOfflinerFolderPath = "${KOGITO_FOLDER_PATH_RELATIVE}/${offlinerFolderName}"
        sh "rsync -rlp --info=progress2 integration-platform-tooling/scripts/rhba/build-offline-repo.sh rhba-prod@${RCM_HOST}:${relativeOfflinerFolderPath}/offline-repo-builder.sh"
    }
    remoteExec("zip -r ${KOGITO_FOLDER_PATH}/rhpam-${RHPAM_VERSION}-kogito-offliner.zip ${KOGITO_FOLDER_PATH}/${offlinerFolderName}")
    remoteExec("rm -rf ${offlinerFolderPath} ${offlinerFile}")
}

def renameArtifact(artifact) {
    def artifactPath = "${KOGITO_FOLDER_PATH}/kogito-${VERSION}-${artifact.originClassifier}"
    def targetArtifactPath = "${KOGITO_FOLDER_PATH}/rhpam-${RHPAM_VERSION}-${artifact.finalClassifier}"
    println "${artifact.info} ${artifactPath} to ${targetArtifactPath}"
    remoteExec("mv ${artifactPath} ${targetArtifactPath}")
}

def generateMavenRepositoryText() {
    println 'Generating maven repository text file'
    def mavenRepoFileName = "rhpam-${RHPAM_VERSION}-kogito-maven-repository"
    remoteExec("unzip -t ${KOGITO_FOLDER_PATH}/${mavenRepoFileName}.zip > ${KOGITO_FOLDER_PATH}/${mavenRepoFileName}.text")
}

def getMilestoneVersion(version) {
    return version.substring(0, version.lastIndexOf('.'))
}

def remoteExec(command) {
    util.withKerberos('rhba-prod-keytab') {
        return sh(script: "ssh rhba-prod@${RCM_HOST} '${command}'", returnStdout: true)
    }
}
