@Library('jenkins-pipeline-shared-libraries')_

def artifacts = [
    [originClassifier: 'maven-repository.zip', finalClassifier: 'maven-repository.zip', stageCommand: 'cp', info: 'Copying maven repository zip'],
    [originClassifier: 'license.zip', finalClassifier: 'licenses.zip', stageCommand: 'cp', info: 'Copying licenses zip'],
    [originClassifier: 'src.zip', finalClassifier: 'sources.zip', stageCommand: 'cp', info: 'Copying sources zip'],
    [originClassifier: 'offliner.zip', finalClassifier: 'offliner.zip', stageCommand: 'mv', info: 'Moving offliner zip']
]

pipeline {
    agent {
        label 'kie-rhel7 && !built-in'
    }

    environment {
        PRODUCT_NAME = PRODUCT_NAME.trim()
        VERSION = VERSION.trim()
        RCM_HOST = RCM_HOST.trim()
        STAGING_BASE_PATH = "${RCM_GUEST_FOLDER}"
        PRODUCT_NAME_LOWER_CASE = PRODUCT_NAME.toLowerCase()
        RHBA_FOLDER_PATH = "${STAGING_BASE_PATH}/rhba/rhba-${VERSION}"
        RHBA_FOLDER_PATH_RELATIVE = "staging/rhba/rhba-${VERSION}"
        TARGET_FOLDER_PATH = "${STAGING_BASE_PATH}/${PRODUCT_NAME_LOWER_CASE}/${PRODUCT_NAME}-${VERSION}"
        TARGET_FOLDER_PATH_RELATIVE = "staging/${PRODUCT_NAME_LOWER_CASE}/${PRODUCT_NAME}-${VERSION}"
    }

    stages {
        stage('Clean workspace') {
            steps {
                cleanWs()
            }
        }

        stage('Print variables') {
            steps {
                println "[INFO] PRODUCT_NAME: ${PRODUCT_NAME}"
                println "[INFO] VERSION: ${VERSION}"
                println "[INFO] RCM_HOST: ${RCM_HOST}"
                println "[INFO] STAGING_BASE_PATH: ${STAGING_BASE_PATH}"
            }
        }

        stage('Clone integration-platform-tooling repository') {
            steps {
                sh "git clone ${INTEGRATION_PLATFORM_TOOLING_REPOSITORY} integration-platform-tooling"
            }
        }

        stage('Create offliner zip') {
            steps {
                script {
                    createOfflinerZip()
                }
            }
        }

        stage('Stage artifacts produced by CPaaS') {
            steps {
                script {
                    for (artifact in artifacts) {
                        stageArtifact(artifact)
                    }
                }
            }
        }

        stage('Generate maven repository text') {
            steps {
                script {
                    generateMavenRepositoryText()
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}

def createOfflinerZip() {
    println 'Creating offliner zip'
    def milestoneVersion = getMilestoneVersion(VERSION)
    def offlinerFolderName = "${PRODUCT_NAME_LOWER_CASE}-${milestoneVersion}-offliner"

    def localOfflinerFolderPath = "${offlinerFolderName}"
    sh "mkdir -p ${localOfflinerFolderPath}"

    util.withKerberos('rhba-prod-keytab') {
        sh "rsync -rlp --info=progress2 rhba-prod@${RCM_HOST}:${RHBA_FOLDER_PATH_RELATIVE}/offliner.txt ${localOfflinerFolderPath}/offliner.txt"
    }

    sh "cp integration-platform-tooling/scripts/rhba/build-offline-repo.sh ${localOfflinerFolderPath}/offline-repo-builder.sh"
    sh "zip -r rhba-${VERSION}-offliner.zip ${localOfflinerFolderPath}"

    util.withKerberos('rhba-prod-keytab') {
        sh "rsync -rlp --info=progress2 rhba-${VERSION}-offliner.zip rhba-prod@${RCM_HOST}:${RHBA_FOLDER_PATH_RELATIVE}"
    }
}

def stageArtifact(artifact) {
    def artifactPath = "${RHBA_FOLDER_PATH}/rhba-${VERSION}-${artifact.originClassifier}"
    def targetArtifactPath = "${TARGET_FOLDER_PATH}/${PRODUCT_NAME_LOWER_CASE}-${getMilestoneVersion(VERSION)}-${artifact.finalClassifier}"
    println "${artifact.info} from ${artifactPath} to ${targetArtifactPath}"
    remoteExec("${artifact.stageCommand} ${artifactPath} ${targetArtifactPath}")
}

def generateMavenRepositoryText() {
    println 'Generating maven repository text file'
    def mavenRepoFileName = "${PRODUCT_NAME_LOWER_CASE}-${getMilestoneVersion(VERSION)}-maven-repository"
    def relativeTargetMavenRepoFileName = "${TARGET_FOLDER_PATH_RELATIVE}/${mavenRepoFileName}"

    // download maven repo zip
    util.withKerberos('rhba-prod-keytab') {
        sh "rsync -rlp --info=progress2 rhba-prod@${RCM_HOST}:${relativeTargetMavenRepoFileName}.zip ${mavenRepoFileName}.zip"
    }

    sh "unzip -t ${mavenRepoFileName}.zip > ${mavenRepoFileName}.text"

    // push maven repo extraction
    util.withKerberos('rhba-prod-keytab') {
        sh "rsync -rlp --info=progress2 ${mavenRepoFileName}.text rhba-prod@${RCM_HOST}:${relativeTargetMavenRepoFileName}.text"
    }
}

def getMilestoneVersion(version) {
    return version.substring(0, version.lastIndexOf('.'))
}

def remoteExec(command) {
    util.withKerberos('rhba-prod-keytab') {
        return sh(script: "ssh rhba-prod@${RCM_HOST} '${command}'", returnStdout: true)
    }
}
