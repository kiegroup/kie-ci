@Library('jenkins-pipeline-shared-libraries')_

def artifacts = [
    [originClassifier: 'optaplanner-quickstarts-sources.zip', finalClassifier: 'optaplanner-quickstarts-sources.zip', info: 'Renaming quickstarts sources zip'],
    [originClassifier: 'optaplanner-sources.zip', finalClassifier: 'optaplanner-sources.zip', info: 'Renaming sources zip'],
    [originClassifier: 'maven-repository.zip', finalClassifier: 'optaplanner-maven-repository.zip', info: 'Renaming maven repository zip'],
    [originClassifier: 'license.zip', finalClassifier: 'optaplanner-product-licenses.zip', info: 'Renaming licenses zip']
]

pipeline {
    agent {
        label 'kie-rhel7 && !built-in'
    }

    environment {
        PRODUCT_NAME = PRODUCT_NAME.trim()
        VERSION = VERSION.trim()
        RCM_HOST = RCM_HOST.trim()
        STAGING_BASE_PATH = "${RCM_GUEST_FOLDER}"
        RHBOP_FOLDER_PATH = "${STAGING_BASE_PATH}/${PRODUCT_NAME}/${PRODUCT_NAME}-${VERSION}"
        RHBOP_FOLDER_PATH_RELATIVE = "staging/${PRODUCT_NAME}/${PRODUCT_NAME}-${VERSION}"
    }

    stages {
        stage('Clean workspace') {
            steps {
                cleanWs()
            }
        }

        stage('Print variables') {
            steps {
                println "[INFO] PRODUCT_NAME: ${PRODUCT_NAME}"
                println "[INFO] VERSION: ${VERSION}"
                println "[INFO] RCM_HOST: ${RCM_HOST}"
                println "[INFO] STAGING_BASE_PATH: ${STAGING_BASE_PATH}"
            }
        }

        stage('Clone integration-platform-tooling repository') {
            steps {
                sh "git clone ${INTEGRATION_PLATFORM_TOOLING_REPOSITORY} integration-platform-tooling"
            }
        }

        stage('Create offliner zip') {
            steps {
                script {
                    createOfflinerZip()
                }
            }
        }

        stage('Rename artifacts produced by CPaaS') {
            steps {
                script {
                    for (artifact in artifacts) {
                        renameArtifact(artifact)
                    }
                }
            }
        }

        stage('Generate maven repository text') {
            steps {
                script {
                    generateMavenRepositoryText()
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}

def renameArtifact(artifact) {
    def artifactPath = "${RHBOP_FOLDER_PATH}/rhbop-${VERSION}-${artifact.originClassifier}"
    def targetArtifactPath = "${RHBOP_FOLDER_PATH}/rhbop-${getMilestoneVersion(VERSION)}-${artifact.finalClassifier}"
    println "${artifact.info} ${artifactPath} to ${targetArtifactPath}"
    remoteExec("mv ${artifactPath} ${targetArtifactPath}")
}

def createOfflinerZip() {
    println 'Creating offliner zip'
    def milestoneVersion = getMilestoneVersion(VERSION)
    def offlinerFolderName = "${PRODUCT_NAME}-${milestoneVersion}-offliner"
    def offlinerFolderPath = "${RHBOP_FOLDER_PATH}/${offlinerFolderName}"
    def offlinerFile = "${RHBOP_FOLDER_PATH}/offliner.txt"

    remoteExec("mkdir -p ${offlinerFolderPath}")
    remoteExec("cp ${offlinerFile} ${offlinerFolderPath}")
    util.withKerberos('rhba-prod-keytab') {
        def relativeOfflinerFolderPath = "${RHBOP_FOLDER_PATH_RELATIVE}/${offlinerFolderName}"
        sh "rsync -rlp --info=progress2 integration-platform-tooling/scripts/rhba/build-offline-repo.sh rhba-prod@${RCM_HOST}:${relativeOfflinerFolderPath}/offline-repo-builder.sh"
    }
    remoteExec("zip -r ${RHBOP_FOLDER_PATH}/rhbop-${milestoneVersion}-optaplanner-offliner.zip ${RHBOP_FOLDER_PATH}/${offlinerFolderName}")
    remoteExec("rm -rf ${offlinerFolderPath} ${offlinerFile}")
}

def generateMavenRepositoryText() {
    println 'Generating maven repository text file'
    def mavenRepoFileName = "${PRODUCT_NAME}-${getMilestoneVersion(VERSION)}-optaplanner-maven-repository"
    remoteExec("unzip -t ${RHBOP_FOLDER_PATH}/${mavenRepoFileName}.zip > ${RHBOP_FOLDER_PATH}/${mavenRepoFileName}.text")
}

def getMilestoneVersion(version) {
    return version.substring(0, version.lastIndexOf('.'))
}

def remoteExec(command) {
    util.withKerberos('rhba-prod-keytab') {
        return sh(script: "ssh rhba-prod@${RCM_HOST} '${command}'", returnStdout: true)
    }
}
